plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.0'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'ie.atu.cloudnative'
version = '0.0.1-SNAPSHOT'
description = 'Project for TextFore API'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(24)
	}
}

repositories {
	mavenCentral()// Add GitHub Packages repository

    maven {
        name = "GitHubPackages"
        // This URL points to the owner/repository where you deployed the package
        url = uri("https://maven.pkg.github.com/cianmacg/TextForge")
        credentials {
            // IMPORTANT: Credentials are required to download from GitHub Packages
            // Use environment variables or local properties for security.
			username = System.getenv("GPR_USER") ?: project.findProperty("gthbUsername")
			password = System.getenv("GPR_TOKEN") ?: project.findProperty("gthbPat")
        }
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'org.springframework.boot:spring-boot-starter-validation' // For @NotNull, etc tags

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-actuator-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-junit-jupiter'

    implementation 'com.github.cianmacg:textforge:1.0.1-SNAPSHOT'
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs.add('--enable-preview')
}

// Target the Spring Boot 'bootRun' task by name and type
tasks.named('bootRun', org.springframework.boot.gradle.tasks.run.BootRun) {
	jvmArgs = ['--enable-preview']
}

tasks.named('test', Test) {
    useJUnitPlatform()
    jvmArgs(['--enable-preview'])
}
